import wollok.game.*
import simulacion.*
import agente.*



object pandemiaGameConfig {
	method height() = 5
	method width() = 5
	method title() = "Simuléichon"
	
	method configureGame() {
		self.configureGameParameters()
		self.agregarManzanas()

		// letra "d" genera un paso de dia
		keyboard.d().onPressDo ({simulacion.pasoDeDia()})
		// letra "c" cura a todos despues del paso del dia
		keyboard.c().onPressDo ({simulacion.curarATodes()})
		
		//letra "i" agrega un infectado a la simulación
		keyboard.i().onPressDo ({simulacion.agregarInfectadoAlAzar()})
		
		// Consulta General
		keyboard.g().onPressDo ({console.println(simulacion.estadoGeneral())})
		
		// Agregar a Agente de Salud
		keyboard.a().onPressDo ({self.agregarAgente()})
		// Sacar Agente de Salud
		keyboard.q().onPressDo ({self.removerAgente()})
		// teclas de acciones del Agente
		keyboard.z().onPressDo ({agenteDeSalud.aislarInfectadoConSintoma()})
		keyboard.x().onPressDo ({agenteDeSalud.convencerRespetar()})

	}
	
	method agregarAgente(){
		game.addVisual(agenteDeSalud)
		agenteDeSalud.configurarFlechas(self.height(),self.width())
		game.onCollideDo(agenteDeSalud, {manzana => game.say(agenteDeSalud, manzana.estadoDeManzana())})
	}
	
	method removerAgente(){
		game.removeVisual(agenteDeSalud)	
	}
		
	method configureGameParameters() {
		game.height(self.height())
		game.width(self.width())
		game.title(self.title())
	}
	
	method agregarManzanas() {
		(1..game.width()).forEach({x => 
			(1..game.height()).forEach({y => 
				const nuevaManzana = simulacion.crearManzana()
				nuevaManzana.position(game.at(x-1,y-1))
				simulacion.agregarManzana(nuevaManzana)
				game.addVisual(nuevaManzana)
			})
		})
	}
}


program pandemiaGame {
	
	pandemiaGameConfig.configureGame()

	
	game.start()
}

